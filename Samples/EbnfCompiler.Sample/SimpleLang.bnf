%TOKENS%
   "var"             = "Var"
   "identifier"      = "Identifier"
   "number"          = "Number"
   "string"          = "String"
   "print"           = "Print"
   "printLine"       = "PrintLine"
   "number-literal"  = "NumberLiteral"
   "string-literal"  = "StringLiteral"
   ";"               = "SemiColon"
   ":"               = "Colon"
   ","               = "Comma"
   "="               = "Assign"
   "("               = "LeftParen"
   ")"               = "RightParen"
   "+"               = "Plus"
   "-"               = "Minus"
   "*"               = "Asterisk"
   "/"               = "ForwardSlash"

%EBNF%
   <statement-list> ::= <statement> #StmtEnd# ";" { <statement> #StmtEnd# ";" } .

   <statement> ::= <var-declaration> | <print-stmt> | <printline-stmt> .

   <var-declaration> ::= #VarStmtStart# "var" #VarStmtIdent# "identifier" ":" #VarStmtType# <type> "=" <expression> #VarStmtEnd# .

   <type> ::= "number" | "string" .

   <expression> ::= [ <sign> ] <term> #UnaryOpEnd# { #BinaryOp# <term-operator> <term> #BinaryOpEnd# } .

   <term> ::= <factor> { #BinaryOp# <factor-operator> <factor> #BinaryOpEnd# } .

   <factor> ::= "(" <expression> ")" | ( #FactIdent# "identifier" ) | <constant-literal> .

   <sign> ::= #UnaryOp# "+" | "-" .

   <term-operator> ::= "+" | "-" .

   <factor-operator> ::= "*" | "/" .

   <constant-literal> ::= ( #NumLiteral# "number-literal" ) | ( #StrLiteral# "string-literal" ) .

   <print-stmt> ::= #PrintStart# "print" "(" <expr-list> ")" .

   <expr-list> ::= <expression> #PrintExprEnd# { "," <expression> #PrintExprEnd# } .

   <printline-stmt> ::= "printLine" "(" [ <expression> ] ")" .