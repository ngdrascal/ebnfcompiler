%TOKENS%
   "var"             = "Var"
   "identifier"      = "Identifier"
   "number"          = "Number"
   "string"          = "String"
   "print"           = "Print"
   "printLine"       = "PrintLine"
   "number-literal"  = "NumberLiteral"
   "string-literal"  = "StringLiteral"
   ";"               = "SemiColon"
   ":"               = "Colon"
   "="               = "Assign"
   "("               = "LeftParen"
   ")"               = "RightParen"
   "+"               = "Plus"
   "-"               = "Minus"
   "*"               = "Asterisk"
   "/"               = "ForwardSlash"

%EBNF%
   <statement-list> ::= <statement> ";" { <statement> ";" } .

   <statement> ::= <var-declaration> | <print-stmt> | <printline-stmt> .

   <var-declaration> ::= #VarStmtStart# "var" #VarStmtIdent# "identifier" ":" #VarStmtType# <type> "=" <expression> #VarStmtEnd# .

   <type> ::= "number" | "string" .

   <expression> ::= #ExprStart# [ <sign> ] <term> { #BinaryOp# <term-operator> <term> } #ExprEnd#.

   <term> ::= #TermStart# <factor> { #BinaryOp# <factor-operator> <factor> } #TermEnd#.

   <factor> ::= "(" <expression> ")" | "identifier" | <constant-literal> .

   <sign> ::= "+" | "-" .

   <term-operator> ::= "+" | "-" .

   <factor-operator> ::= "*" | "/" .

   <constant-literal> ::= ( #NumLiteral# "number-literal" ) | "string-literal" .

   <print-stmt> ::= "print" "(" <expression> ")".

   <printline-stmt> ::= "printLine" "(" [ <expression> ] ")" .